buildscript {
    ext.kotlin_version = '1.2.71'
    repositories {
        maven { url 'https://jitpack.io' }
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.sankuai.waimai.router:plugin:1.1.0'

        if (project.findProject(':buildSrc') == null) {
            classpath 'com.github.bingoogolapple.AppInit:buildSrc:1.0.8'
        }
    }
}

allprojects {
    repositories {
        maven {
            url uri("${System.properties['user.home']}/.gradle/local_repo")
        }
        maven { url 'https://jitpack.io' }
        jcenter()
        google()
    }

    tasks.withType(JavaCompile) { options.encoding = "UTF-8" }
}

// 配置 demo
subprojects {
    afterEvaluate { project ->
        ext.pluginContainer = it.getPlugins()
        // 统一配置 Android App 和 Android Library 公共参数
        if (ext.pluginContainer.hasPlugin("com.android.application") || ext.pluginContainer.hasPlugin("com.android.library")) {
            android {
                compileSdkVersion 28

                defaultConfig {
                    minSdkVersion 16
                    //noinspection ExpiredTargetSdkVersion
                    targetSdkVersion 23
                    versionCode 100
                    versionName '1.0.0'
                    if (project.file('consumer-proguard-rules.pro').exists()) {
                        consumerProguardFiles 'consumer-proguard-rules.pro'
                    }
                }

                compileOptions {
                    sourceCompatibility '1.8'
                    targetCompatibility '1.8'
                }
            }
        }

        // 统一配置 Java、groovy、kotlin 公共参数
        if (ext.pluginContainer.hasPlugin("java") || ext.pluginContainer.hasPlugin("groovy") || ext.pluginContainer.hasPlugin("kotlin")) {
            sourceCompatibility = '1.8'
            targetCompatibility = '1.8'
        }
    }

    def libProjectList = ['common', 'api', 'compiler', 'buildSrc']
    if (libProjectList.contains(it.name)) {
        return
    }

    if (it.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    // 应用 Kotlin 插件，用于测试对 Kotlin 的支持
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'
    // 应用 AppInit 插件。如果有用到 Kotlin，需要放到 Kotlin 插件之后
    apply plugin: 'bga-appinit-plugin'

    // 测试多 flavor 的情况
//    android {
//        /**
//         * money 场景，免费版和收费版
//         * channel 场景，不同的市场渠道
//         */
//        flavorDimensions 'money', 'channel'
//
//        productFlavors {
//            free {
//                dimension 'money'
//            }
//            vip {
//                dimension 'money'
//            }
//            baidu {
//                dimension 'channel'
//            }
//            xiaomi {
//                dimension 'channel'
//            }
//        }
//    }

    dependencies {
        // android support
        implementation 'com.android.support:appcompat-v7:28.0.0'
        implementation 'com.android.support:support-v4:28.0.0'
        // 用于测试对 Kotlin 的支持
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        // 美团外卖 Router 相关，用于测试多模块
        implementation 'com.sankuai.waimai.router:router:1.1.2'
        annotationProcessor 'com.sankuai.waimai.router:compiler:1.1.2'
        kapt 'com.sankuai.waimai.router:compiler:1.1.2'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
